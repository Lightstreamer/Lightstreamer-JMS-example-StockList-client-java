plugins {
    id 'application'
    id 'com.github.sherter.google-java-format' version '0.9'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
      url 'https://www.lightstreamer.com/repo/maven'
    }
}


// Set default broker if not specified from command line
def whichBroker = project.properties['broker']?:'ActiveMQ'

dependencies {
    implementation 'com.lightstreamer:ls-jms-javase-client:1.0.0-alpha-02'
    runtime 'org.slf4j:slf4j-simple:1.7.28'
    implementation 'com.beust:jcommander:1.78'

    switch (whichBroker) {
        case "ActiveMQ":
            runtimeOnly 'org.apache.activemq:activemq-client:5.15.12'
            runtimeOnly 'org.apache.geronimo.specs:geronimo-j2ee-management_1.1_spec:1.0.1'
            break;

        case "Artemis":
            runtimeOnly 'org.apache.activemq:artemis-jms-client-all:2.11.0'
            break;

        case "TIBCO":
            runtimeOnly files('local_libs/tibjms.jar')
            break;

        case "IBM_MQ":
            runtimeOnly 'com.ibm.mq:com.ibm.mq.allclient:9.1.3.0'
            runtimeOnly files('local_libs/fscontext.jar','local_libs/providerutil.jar')
            break;

        case "WebLogic":
            runtimeOnly files('local_libs/wlthint3client.jar')
            break;


        /* In case of different broker, please add a new 'case' here:
        case "<JMS_BROKER>":
            runtimeOnly '<DEP1>'
            ...
            runtimeOnly '<DEP_N>'

            break;

        where:
            JMS_BROKER
                is a generic string that identifies the broker for which this build is targeted.
            DEP_X
                is the reference to the JMS broker-specific java library. If the library is not available
                on JCenter, please copy it under the "local_libs" folder, as already done with TIBCO.
        */

        default:
            println "No broker definition"
    }
}


application  {
    mainClass = 'com.lightstreamer.jms.demo.stocklist_client.App'
    sourceCompatibility = '1.8'
}

build.dependsOn verifyGoogleJavaFormat